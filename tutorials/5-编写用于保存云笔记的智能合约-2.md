[TOC]

---

### 任务描述

本关任务：编写以太坊云笔记小程序使用的智能合约，实现对云笔记的新增、编辑、查看功能。

### 相关知识

#### 子货币（Subcurrency）例子

下面的合约实现了一个最简单的加密货币。这里，币确实可以无中生有地产生，但是只有创建合约的人才能做到（实现一个不同的发行计划也不难）。而且，任何人都可以给其他人转币，不需要注册用户名和密码 —— 所需要的只是以太坊密钥对。

```solidity
pragma solidity >=0.5.0;

contract Coin {
    // 关键字“public”让这些变量可以从外部读取
    address public minter;
    mapping (address => uint) public balances;

    // 轻客户端可以通过事件针对变化作出高效的反应
    event Sent(address from, address to, uint amount);

    // 这是构造函数，只有当合约创建时运行
    constructor() public {
        minter = msg.sender;
    }

    function mint(address receiver, uint amount) public {
        if (msg.sender != minter) return;
        balances[receiver] += amount;
    }

    function send(address receiver, uint amount) public {
        if (balances[msg.sender] < amount) return;
        balances[msg.sender] -= amount;
        balances[receiver] += amount;
        emit Sent(msg.sender, receiver, amount);
    }
}
```

这个合约引入了一些新的概念，让我们逐一解读。

`address public minter;` 这一行声明了一个可以被公开访问的 `address` 类型的状态变量。 `address` 类型是一个160位的值，且不允许任何算数操作。这种类型适合存储合约地址或外部人员的密钥对。关键字 `public` 自动生成一个函数，允许你在这个合约之外访问这个状态变量的当前值。如果没有这个关键字，其他的合约没有办法访问这个变量。由编译器生成的函数的代码大致如下所示：

```
function minter() returns (address) { return minter; }
```

当然，加一个和上面完全一样的函数是行不通的，因为我们会有同名的一个函数和一个变量，这里，主要是希望你能明白——编译器已经帮你实现了。

下一行， `mapping (address => uint) public balances;` 也创建一个公共状态变量，但它是一个更复杂的数据类型。 该类型将address映射为无符号整数。 Mappings 可以看作是一个`哈希表`它会执行虚拟初始化，以使所有可能存在的键都映射到一个字节表示为全零的值。 但是，这种类比并不太恰当，因为它既不能获得映射的所有键的列表，也不能获得所有值的列表。 因此，要么记住你添加到mapping中的数据（使用列表或更高级的数据类型会更好），要么在不需要键列表或值列表的上下文中使用它，就如本例。 而由 `public` 关键字创建的getter函数getter function则是更复杂一些的情况， 它大致如下所示：

```
function balances(address _account) public view returns (uint) {
    return balances[_account];
}
```

正如你所看到的，你可以通过该函数轻松地查询到账户的余额。

`event Sent(address from, address to, uint amount);` 这行声明了一个所谓的`事件（event）`，它会在 `send` 函数的最后一行被发出。用户界面（当然也包括服务器应用程序）可以监听区块链上正在发送的事件，而不会花费太多成本。一旦它被发出，监听该事件的listener都将收到通知。而所有的事件都包含了 `from` ， `to` 和 `amount` 三个参数，可方便追踪事务。 为了监听这个事件，你可以使用如下代码：

```
Coin.Sent().watch({}, '', function(error, result) {
    if (!error) {
        console.log("Coin transfer: " + result.args.amount +
            " coins were sent from " + result.args.from +
            " to " + result.args.to + ".");
        console.log("Balances now:\n" +
            "Sender: " + Coin.balances.call(result.args.from) +
            "Receiver: " + Coin.balances.call(result.args.to));
    }
})
```

这里请注意自动生成的 `balances` 函数是如何从用户界面调用的。

特殊函数 `constructor()` 是在创建合约期间运行的构造函数，不能在事后调用。 它永久存储创建合约的人的地址: `msg` (以及 `tx` 和 `block` ) 是一个神奇的全局变量，其中包含一些允许访问区块链的属性。 `msg.sender` 始终是当前（外部）函数调用的来源地址。

最后，真正被用户或其他合约所调用的，以完成本合约功能的方法是 `mint` 和 `send`。 如果 `mint` 被合约创建者外的其他人调用则什么也不会发生。 另一方面， `send` 函数可被任何人用于向他人发送币 (当然，前提是发送者拥有这些币)。记住，如果你使用合约发送币给一个地址，当你在区块链浏览器上查看该地址时是看不到任何相关信息的。因为，实际上你发送币和更改余额的信息仅仅存储在特定合约的数据存储器中。通过使用事件，你可以非常简单地为你的新币创建一个“区块链浏览器”来追踪交易和余额。

#### 编写用于保存云笔记的智能合约

经过前面的介绍，你可能已大致了解了一个智能合约基本要素，在本项目`《支持以太坊的小程序云笔记开发》`中，需要使用以太坊网络来存储笔记内容。使用以太坊存储需要编写一个智能合约，来规定网络中的计算机如何存储笔记。通过编写智能合约，实现保存/编辑笔记，及查看笔记的功能。

简单来说，云笔记智能合约是需要将笔记内容保存到合约中的映射变量中。由于合约的成员变量在默认情况下是以store形式存储的，所以映射中的所有数据都会永久存储在区块链上。

下面，我们来编写一个智能合约。其通过`editNote`函数发布和更新笔记，通过`getNote`函数获取笔记内容。

当前，笔者使用的`solidity`的版本为`0.5.1`。

##### 指定solidity版本

```solidity
pragma solidity >=0.5.0;
```

##### 新建智能合约`BlockchainNoteService`

```solidity
contract BlockchainNoteService
{
}
```

##### 定义结构体，用来存储单条笔记的数据

```solidity
    struct Note{
        string title;//笔记标题
        string content;//笔记内容
    }
```

##### 设置存储映射

使用映射`mapping`，建立键－值的对应关系，可以通过键来查找值，键必须是唯一的，但值可以重复。在本案例中，建立笔记`id`到笔记内容`Note`结构体，用户`id` `msg.sender`到笔记`id`的映射，这个映射的名字是`notedata`。

```solidity
mapping(address => mapping(uint256 => Note)) notedata;
```

##### 定义新增/编辑笔记方法

在本应用案例中，新增和编辑方法类似，下面定义一个`editNote`类内方法。

```sodility
    function editNote(uint256 id,  string memory title, string memory content)  public
    {
        require(id > 0, "id不能为空");
        require(keccak256(abi.encodePacked(title))!=keccak256(abi.encodePacked("")), "title不能为空");
        require(keccak256(abi.encodePacked(content))!=keccak256(abi.encodePacked("")), "content不能为空");
        notedata[msg.sender][id]= Note(title, content);
    }
```

**注意，在定义`string`类型时，需要在类型后指定存储位置，在案例中，指定为`memory`。**

##### 定义读取笔记方法

```solidity
    function getNote(uint256 id) public view returns(string memory, string memory)
    {
        require(id > 0, "id不能为空");
        return (notedata[msg.sender][id].title, notedata[msg.sender][id].content);
    }
```

经过以上步骤，一个用于保存云笔记的智能合约已经建立。

### 编程要求

请在右侧的编辑器中，直接编辑修改文件，具体要求是：

实现合约中查看区块链上云笔记的功能，其输入为`uint256`类型的云笔记标号`id`；其中使用`require` 函数用于确认`id`条件有效性，有两个返回值，返回值的第一个参数为`string`类型的云笔记标题，第二个为`string`类型的云笔记内容`content`。

**注意，`string`类型需要指定存储类型。**

### 测试说明

根据相关知识，按照要求完成右区域内的代码。作答完毕，通过点击“评测”，可以验证程序功能的正确性。

---

开始你的任务吧，祝你成功！